set(NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME nvjpeg2k_ext)

set(NVIMGCDCS_NVJPEG2K_EXT_SRC
  nvjpeg2k_ext.cpp
  cuda_encoder.cpp
  cuda_decoder.cpp
)

add_library(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME} SHARED ${NVIMGCDCS_NVJPEG2K_EXT_SRC} ext_module.cpp)
add_library(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}_static STATIC ${NVIMGCDCS_NVJPEG2K_EXT_SRC})

if(NOT WITH_DYNAMIC_NVJPEG2K)
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PUBLIC ${NVJPEG2K_LIBRARY})
else()
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE dynlink_nvjpeg2k)
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE "-Wl,--exclude-libs,$<TARGET_FILE_NAME:dynlink_nvjpeg2k>")
endif()

if (WITH_DYNAMIC_NPP)
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE dynlink_npp)
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE "-Wl,--exclude-libs,$<TARGET_FILE_NAME:dynlink_npp>")
else()
  target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    PUBLIC CUDA::nppc_static CUDA::nppidei_static)
endif()

target_link_libraries(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME} PUBLIC
  CUDA::cudart_static)

if(UNIX)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -fPIC -fvisibility=hidden")
  set_target_properties(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME} PROPERTIES
    POSITION_INDEPENDENT_CODE ON
    VERSION ${PROJECT_VERSION}
    SOVERSION ${PROJECT_VERSION_MAJOR})
else()
  set_target_properties(${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME} PROPERTIES
    POSITION_INDEPENDENT_CODE ON
    VERSION ${PROJECT_VERSION}
    OUTPUT_NAME ${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}_${PROJECT_VERSION_MAJOR}
    ARCHIVE_OUTPUT_NAME ${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME})
endif()

if(UNIX)
  install(TARGETS ${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    LIBRARY DESTINATION extensions
    PUBLIC_HEADER DESTINATION include
  )

else()
  install(TARGETS ${NVIMGCDCS_NVJPEG2K_EXT_LIBRARY_NAME}
    RUNTIME DESTINATION extensions
    LIBRARY DESTINATION lib64
    ARCHIVE DESTINATION lib64
    PUBLIC_HEADER DESTINATION include
  )
endif()
