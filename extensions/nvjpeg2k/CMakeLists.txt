set(NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME nvjpeg2k_ext)

set(NVIMGCODEC_NVJPEG2K_EXT_SRC
  nvjpeg2k_ext.cpp
  cuda_encoder.cpp
  cuda_decoder.cpp
  exception.cpp
)

add_library(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME} SHARED ${NVIMGCODEC_NVJPEG2K_EXT_SRC} ext_module.cpp $<TARGET_OBJECTS:nvimgcodec_imgproc_static>)
add_library(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}_static STATIC ${NVIMGCODEC_NVJPEG2K_EXT_SRC} $<TARGET_OBJECTS:nvimgcodec_imgproc_static>)

if(NOT WITH_DYNAMIC_NVJPEG2K)
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PUBLIC ${NVJPEG2K_LIBRARY})
else()
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE dynlink_nvjpeg2k)
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE "-Wl,--exclude-libs,$<TARGET_FILE_NAME:dynlink_nvjpeg2k>")
endif()

if (WITH_DYNAMIC_NPP)
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE dynlink_npp)
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PRIVATE "-Wl,--exclude-libs,$<TARGET_FILE_NAME:dynlink_npp>")
else()
  target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    PUBLIC CUDA::nppc_static CUDA::nppidei_static CUDA::nppicc_static)
endif()

target_link_libraries(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME} PUBLIC
  CUDA::cudart_static)

if(UNIX)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -fPIC -fvisibility=hidden")
  set_target_properties(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME} PROPERTIES
    POSITION_INDEPENDENT_CODE ON
    VERSION ${PROJECT_VERSION}
    NO_SONAME OFF)
else()
  set_target_properties(${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME} PROPERTIES
    POSITION_INDEPENDENT_CODE ON
    VERSION ${PROJECT_VERSION}
    OUTPUT_NAME ${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}_${PROJECT_VERSION_MAJOR}
    ARCHIVE_OUTPUT_NAME ${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME})
endif()

if(UNIX)
  install(TARGETS ${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME} ${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}_static
    LIBRARY DESTINATION extensions NAMELINK_SKIP COMPONENT lib
    ARCHIVE DESTINATION lib64 COMPONENT lib
    PUBLIC_HEADER DESTINATION include COMPONENT lib
  )

else()
  install(TARGETS ${NVIMGCODEC_NVJPEG2K_EXT_LIBRARY_NAME}
    RUNTIME DESTINATION extensions COMPONENT lib
    LIBRARY DESTINATION lib64 COMPONENT lib
    ARCHIVE DESTINATION lib64 COMPONENT lib
    PUBLIC_HEADER DESTINATION include COMPONENT lib
  )
endif()
